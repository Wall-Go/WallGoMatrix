(****************************
        general info
****************************)
M$ModelName = "n-scalars";

M$Information = {
Authors -> {"O. Gould"},
Institutions -> {"U. Nottingham"},
Emails -> {"oliver.gould@nottingham.ac.uk"},
Date -> "January 27th, 2025"
};

(****************************
           indices
****************************)

n = 5;
IndexRange[ Index[Flavour] ] = Range[n];
IndexStyle[ Flavour, a];

(****************************
        fields
****************************)
M$ClassesDescription = {
    S[1] == {
        ClassName -> Phi,
        ClassMembers -> Array[Phi, n],
        PropagatorLabel -> Phi,
        SelfConjugate -> True,
        Indices -> {Index[Flavour]},
        FlavorIndex -> Flavour,
        Mass -> Array[MassPhi, n] 
    }
};

(****************************
        parameters
****************************)
M$Parameters = {
    lam == {
        ParameterType -> External,
        ComplexParameter -> False,
        Indices -> {Index[Flavour], Index[Flavour], Index[Flavour], Index[Flavour]},
        Description -> "Quartic couplings"
    },
    g == {
        ParameterType -> External,
        ComplexParameter -> False,
        Indices -> {Index[Flavour], Index[Flavour], Index[Flavour]},
        Description -> "Cubic couplings"
    },
    msq == {
        ParameterType -> External,
        ComplexParameter -> False,
        Indices -> {Index[Flavour], Index[Flavour]},
        Description -> "Masses squared"
    }
};

(****************************
        lagrangian
****************************)
L := Module[{T, V},

    T = (
        (1/2) del[Phi[a], mu] del[Phi[a], mu]
    );
    V = (
        1 / 2 msq[a1, a2] Phi[a1] Phi[a2] +
        1 / 3! g[a1, a2, a3] Phi[a1] Phi[a2] Phi[a3] +
        1 / 4! lam[a1, a2, a3, a4] Phi[a1] Phi[a2] Phi[a3] Phi[a4]
    );

    T - V
];
